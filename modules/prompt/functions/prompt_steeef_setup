#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#
prompt_width () {
  echo $(( ${#${(S%%)1//(\%([KF1]|)\{*\}|\%[Bbkf])}} ))
}
local COLUMNS=$(tput cols)

function prompt_steeef_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="${_prompt_steeef_colors[1]}(%b)%f%u%c${_prompt_steeef_colors[4]} untracked%f"
  else
    branch_format="${_prompt_steeef_colors[1]}(%b)%f%u%c"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
  # Define prompts.
  # local tmux_available=''
  # [[ -z $TMUX ]] && tmux_available="${_prompt_steeef_colors[6]}◢!◣ %f"
  local line1_left="%F{white}-- %f${tmux_available}${_prompt_steeef_colors[3]}%n%f in ${_prompt_steeef_colors[5]}%~%f "
  local line1_right=" ${vcs_info_msg_0_} %F{white}--%f"
  local line1_left_width="$(prompt_width $line1_left)"
  local line1_right_width=$(prompt_width $line1_right)
  local filler="%{%F{white}%}${(l:$(( $COLUMNS - ( 1 + $line1_left_width + $line1_right_width ) ))::—:)}%{$reset_color%}"
  local line2_left="$python_info[virtualenv]ツ "

  PROMPT="
$line1_left$filler$line1_right
$line2_left"
}

function prompt_steeef_setup {

  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_steeef_precmd
  # add-zsh-hook precmd prompt_steeef_line

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_steeef_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
      "%F{red}"
    )
  else
    _prompt_steeef_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_steeef_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_steeef_colors[5]}%a%f)"
  local unstaged_format="${_prompt_steeef_colors[2]} unstaged%f" # Used to be this char ●
  local staged_format="${_prompt_steeef_colors[5]} staged%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  # TODO For when I know how to fix double-sidth chars: (╯°∿°)╯︵ ┻━┻
  zstyle ':vcs_info:*:prompt:*' nvcsformats   "${_prompt_steeef_colors[2]}(╯°∿°)╯ ┻━┻%f"

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

}

prompt_steeef_setup "$@"
